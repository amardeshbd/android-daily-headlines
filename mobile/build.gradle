apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    // https://developer.android.com/studio/releases/build-tools.html
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "info.hossainkhan.dailynewsheadlines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 3
        versionName "1.0-mobile"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        /*
         * `InvalidPackage` warning from dagger. Fixed issue using following sources:
         * http://stackoverflow.com/questions/21827004/gradle-dagger-lint-ignore-by-package
         * https://github.com/square/okio/issues/58
         */
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibilityVersion
        targetCompatibility rootProject.ext.javaTargetCompatibilityVersion
    }
}

jacocoAndroidUnitTestReport {
    excludes += ['**/AutoValue_*.*',
                 '**/*$ViewBinder*.*', /* Based on https://github.com/JakeWharton/butterknife/issues/289 */
                 '**/*JavascriptBridge.class']
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Core module for the app
    implementation project(':core-lib')

    // Android Support Lib
    implementation "com.android.support:support-v13:$rootProject.supportLibraryVersion"
    implementation "com.android.support:design:$rootProject.supportLibraryVersion"

    // Firebase CORE
    // https://firebase.google.com/docs/android/setup
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseVersion"

    // Firebase - Crash Reporting
    // https://firebase.google.com/docs/crash/android
    implementation "com.google.firebase:firebase-crash:$rootProject.firebaseVersion"

    // Butterknife - android-view injection
    implementation "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"

    // Logging framework
    // https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"

    // Picasso - Fast and memory efficient image loading
    // http://square.github.io/picasso/
    implementation "com.squareup.picasso:picasso:$rootProject.picassoVersion"

    // Gson for JSON serialization and deserialization
    // https://github.com/google/gson
    implementation "com.google.code.gson:gson:$rootProject.gsonVersion"

    // RxJava
    // https://github.com/ReactiveX/RxJava
    implementation "io.reactivex:rxjava:$rootProject.rxJavaVersion"

    // RxAndroid (Note: RxJava is included in `api-lib` module)
    // https://github.com/ReactiveX/RxAndroid
    implementation "io.reactivex:rxandroid:$rootProject.rxAndroidVersion"

    implementation "com.android.support.constraint:constraint-layout:1.0.2"

    // ----------------------------------------------------------------
    // Android Unit and Instrumentation test
    // ----------------------------------------------------------------
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation "junit:junit:$rootProject.junitVersion"

    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
}

// **IMPORTANT** Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'
