apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    // https://developer.android.com/studio/releases/build-tools.html
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "info.hossainkhan.dailynewsheadlines"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 6
        versionName "1.2-tv"
        versionNameSuffix "-${gitSha()}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        jackOptions {
            enabled true
        }
    }
    buildTypes {
        debug {
            // Turn off optimization for faster debug builds
            shrinkResources false
            minifyEnabled false
        }

        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        /*
         * `InvalidPackage` warning from dagger. Fixed issue using following sources:
         * http://stackoverflow.com/questions/21827004/gradle-dagger-lint-ignore-by-package
         * https://github.com/square/okio/issues/58
         */
        warning 'InvalidPackage'
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibilityVersion
        targetCompatibility rootProject.ext.javaTargetCompatibilityVersion
    }
}

jacocoAndroidUnitTestReport {
    excludes += ['**/AutoValue_*.*',
                 '**/*$ViewBinder*.*', /* Based on https://github.com/JakeWharton/butterknife/issues/289 */
                 '**/*JavascriptBridge.class']
}


def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def buildTime() {
    def df = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // Core module for the app
    compile project(':core-lib')

    compile "com.android.support:leanback-v17:$rootProject.supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$rootProject.supportLibraryVersion"

    // Needed for leanback preference
    compile "com.android.support:preference-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-leanback-v17:$rootProject.supportLibraryVersion"
    compile "com.android.support:preference-v14:$rootProject.supportLibraryVersion"

    // Firebase CORE
    // https://firebase.google.com/docs/android/setup
    compile "com.google.firebase:firebase-core:$rootProject.firebaseVersion"

    // Firebase - Crash Reporting
    // https://firebase.google.com/docs/crash/android
    compile "com.google.firebase:firebase-crash:$rootProject.firebaseVersion"

    // Butterknife - android-view injection
    compile "com.jakewharton:butterknife:$rootProject.butterKnifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnifeVersion"
}


// **IMPORTANT** Add to the bottom of the file
apply plugin: 'com.google.gms.google-services'